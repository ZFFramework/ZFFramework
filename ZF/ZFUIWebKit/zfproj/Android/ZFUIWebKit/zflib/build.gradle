plugins {
    alias(libs.plugins.android.library)
}

def ZF_ROOT_PATH = this.projectDir.getAbsolutePath() + '/../../../../../../../ZFFramework'
android {
    namespace 'com.ZFFramework.ZFUIWebKit'
    compileSdk 35

    defaultConfig {
        minSdk 21

        externalNativeBuild {
            cmake {
                cppFlags ''
            }
        }
        ndk {
            if (System.getenv('ZF_ANDROID_ABI') != null && !System.getenv('ZF_ANDROID_ABI').isEmpty()) {
                // ZF_ANDROID_ABI=armeabi-v7a,arm64-v8a,x86 ./gradle assembleRelease
                abiFilters System.getenv('ZF_ANDROID_ABI').split(',')
            } else {
                // abiFilters 'arm64-v8a', 'armeabi', 'armeabi-v7a', 'mips', 'mips64', 'x86', 'x86_64'
                abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86'
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    sourceSets {
        main {
            java.srcDirs += this.projectDir.getAbsolutePath() + '/../../../../zfsrc/ZFImpl/sys_Android/java'
        }
    }
    externalNativeBuild {
        cmake {
            path 'CMakeLists.txt'
        }
    }
}

dependencies {
    implementation fileTree(includes: ['*.jar'], dir: 'libs')
    if (file(this.projectDir.getAbsolutePath() + '/zfprojConfig/zfprojConfig.xml').exists()) {
        implementation project(':zfprojConfig_ZFUIWebKit')
    }
    compileOnly fileTree(includes: ['*.jar'], dir: ZF_ROOT_PATH + '/_release/Android/module/ZFCore/src/main/libs')
    compileOnly fileTree(includes: ['*.jar'], dir: ZF_ROOT_PATH + '/_release/Android/module/ZFAlgorithm/src/main/libs')
    compileOnly fileTree(includes: ['*.jar'], dir: ZF_ROOT_PATH + '/_release/Android/module/ZFUtility/src/main/libs')
    compileOnly fileTree(includes: ['*.jar'], dir: ZF_ROOT_PATH + '/_release/Android/module/ZFUIKit/src/main/libs')
}

if (file(this.projectDir.getAbsolutePath() + '/zfprojConfig/zfprojConfig.gradle').exists()) {
    apply from: this.projectDir.getAbsolutePath() + '/zfprojConfig/zfprojConfig.gradle'
}

import org.apache.tools.ant.taskdefs.condition.Os

static def ZFCmd(cmd, args) {
    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
        return ['cmd.exe', '/c', "${cmd}.bat"] + args
    } else {
        return ['sh', "${cmd}.sh"] + args
    }
}

tasks.register('ZFCopyRes_ZFUIWebKit', Exec) {
    commandLine ZFCmd(ZF_ROOT_PATH + '/tools/util/copy_res', [
            this.projectDir.getAbsolutePath() + '/../../../../zfres',
            this.projectDir.getAbsolutePath() + '/src/main/assets/zfres'
    ])
}
tasks.matching { it.name.startsWith('pre') && it.name.endsWith('Build') }.configureEach {
    dependsOn('ZFCopyRes_ZFUIWebKit')
}

tasks.register('ZFCopyLib_ZFUIWebKit', Exec) {
    commandLine ZFCmd(ZF_ROOT_PATH + '/tools/spec/Android/libs_copy', [
            'ZFUIWebKit',
            this.projectDir.getAbsolutePath() + '/..',
    ])
}
tasks.matching { it.name.startsWith('assemble') }.configureEach {
    finalizedBy('ZFCopyLib_ZFUIWebKit')
}

