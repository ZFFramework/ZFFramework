plugins {
    alias(libs.plugins.android.library)
}

def ZF_ROOT_PATH = this.projectDir.getAbsolutePath() + '/../../../../../../../ZFFramework'
android {
    namespace 'com.ZFFramework.ZFAppUtil'
    compileSdk 35

    defaultConfig {
        minSdk 21

        externalNativeBuild {
            cmake {
                cppFlags ''
            }
        }
        ndk {
            if (System.getenv('ZF_ANDROID_ABI') != null && !System.getenv('ZF_ANDROID_ABI').isEmpty()) {
                // ZF_ANDROID_ABI=armeabi-v7a,arm64-v8a,x86 ./gradle assembleRelease
                abiFilters System.getenv('ZF_ANDROID_ABI').split(',')
            } else {
                // abiFilters 'arm64-v8a', 'armeabi', 'armeabi-v7a', 'mips', 'mips64', 'x86', 'x86_64'
                abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86'
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    sourceSets {
        main {
            java.srcDirs += this.projectDir.getAbsolutePath() + '/../../../../zfsrc/ZFImpl/sys_Android/java'
            assets.srcDirs += ['assets']
        }
    }
    externalNativeBuild {
        cmake {
            path 'CMakeLists.txt'
        }
    }
}

dependencies {
    implementation fileTree(includes: ['*.jar'], dir: 'libs')
    if (file(this.projectDir.getAbsolutePath() + '/zfprojConfig.xml').exists()) {
        implementation project(':zfprojConfig:ZFAppUtil')
    }
    implementation fileTree(includes: ['*.jar'], dir: ZF_ROOT_PATH + '/_release/Android/module/ZFCore/src/main/libs')
    implementation fileTree(includes: ['*.jar'], dir: ZF_ROOT_PATH + '/_release/Android/module/ZFAlgorithm/src/main/libs')
    implementation fileTree(includes: ['*.jar'], dir: ZF_ROOT_PATH + '/_release/Android/module/ZFUtility/src/main/libs')
    implementation fileTree(includes: ['*.jar'], dir: ZF_ROOT_PATH + '/_release/Android/module/ZFUIKit/src/main/libs')
    implementation fileTree(includes: ['*.jar'], dir: ZF_ROOT_PATH + '/_release/Android/module/ZFUIWidget/src/main/libs')
    implementation fileTree(includes: ['*.jar'], dir: ZF_ROOT_PATH + '/_release/Android/module/ZFUIExt/src/main/libs')
    implementation fileTree(includes: ['*.jar'], dir: ZF_ROOT_PATH + '/_release/Android/module/ZFLua/src/main/libs')
    implementation fileTree(includes: ['*.jar'], dir: ZF_ROOT_PATH + '/_release/Android/module/ZFUIWebKit/src/main/libs')
    implementation fileTree(includes: ['*.jar'], dir: ZF_ROOT_PATH + '/_release/Android/module/ZFNet/src/main/libs')
}

if (file(this.projectDir.getAbsolutePath() + '/zfprojConfig.gradle').exists()) {
    apply from: this.projectDir.getAbsolutePath() + '/zfprojConfig.gradle'
}

import org.apache.tools.ant.taskdefs.condition.Os

task ZFSetup {
    doLast {
        if (Os.isFamily(Os.FAMILY_WINDOWS)) {
            exec {
                executable 'cmd.exe'
                args '/c', ZF_ROOT_PATH + '/tools/util/copy_res.bat', this.projectDir.getAbsolutePath() + '/../../../../zfres', this.projectDir.getAbsolutePath() + '/src/main/assets/zfres'
            }
        } else {
            exec {
                executable 'sh'
                args ZF_ROOT_PATH + '/tools/util/copy_res.sh', this.projectDir.getAbsolutePath() + '/../../../../zfres', this.projectDir.getAbsolutePath() + '/src/main/assets/zfres'
            }
        }
    }
}
preBuild.dependsOn ZFSetup

gradle.buildFinished {
    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
        exec {
            executable 'cmd.exe'
            args '/c', ZF_ROOT_PATH + '/tools/spec/Android/libs_copy.bat', 'ZFAppUtil', this.projectDir.getAbsolutePath() + '/..'
        }
    } else {
        exec {
            executable 'sh'
            args ZF_ROOT_PATH + '/tools/spec/Android/libs_copy.sh', 'ZFAppUtil', this.projectDir.getAbsolutePath() + '/..'
        }
    }
}

