plugins {
    alias(libs.plugins.android.library)
}

def ZF_ROOT_PATH = this.projectDir.getAbsolutePath() + '/../../../../../../../ZFFramework'
android {
    namespace 'com.ZFFramework.ZFUIWidget'
    compileSdk 35

    defaultConfig {
        minSdk 21

        externalNativeBuild {
            cmake {
                cppFlags ''
            }
        }
        ndk {
            if (System.getenv('ZF_ANDROID_ABI') != null && !System.getenv('ZF_ANDROID_ABI').isEmpty()) {
                // ZF_ANDROID_ABI=armeabi-v7a,arm64-v8a,x86 ./gradle assembleRelease
                abiFilters System.getenv('ZF_ANDROID_ABI').split(',')
            } else {
                // abiFilters 'arm64-v8a', 'armeabi', 'armeabi-v7a', 'mips', 'mips64', 'x86', 'x86_64'
                abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86'
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    sourceSets {
        main {
            java.srcDirs += this.projectDir.getAbsolutePath() + '/../../../../zfsrc/ZFImpl/sys_Android/java'
            assets.srcDirs += ['assets']
            if (file(this.projectDir.getAbsolutePath() + '/zfprojConfig/zfprojConfig.xml').exists()) {
                manifest.srcFile this.projectDir.getAbsolutePath() + '/zfprojConfig/zfprojConfig.xml'
            }
        }
    }
    externalNativeBuild {
        cmake {
            path 'CMakeLists.txt'
        }
    }
}

dependencies {
    implementation fileTree(includes: ['*.jar'], dir: 'libs')
    compileOnly fileTree(includes: ['*.jar'], dir: ZF_ROOT_PATH + '/_release/Android/module/ZFCore/src/main/libs')
    compileOnly fileTree(includes: ['*.jar'], dir: ZF_ROOT_PATH + '/_release/Android/module/ZFAlgorithm/src/main/libs')
    compileOnly fileTree(includes: ['*.jar'], dir: ZF_ROOT_PATH + '/_release/Android/module/ZFUtility/src/main/libs')
    compileOnly fileTree(includes: ['*.jar'], dir: ZF_ROOT_PATH + '/_release/Android/module/ZFUIKit/src/main/libs')
}

if (file(this.projectDir.getAbsolutePath() + '/zfprojConfig/zfprojConfig.gradle').exists()) {
    apply from: this.projectDir.getAbsolutePath() + '/zfprojConfig/zfprojConfig.gradle'
}

import org.apache.tools.ant.taskdefs.condition.Os

task ZFSetup {
    doLast {
        if (Os.isFamily(Os.FAMILY_WINDOWS)) {
            exec {
                executable 'cmd.exe'
                args '/c', ZF_ROOT_PATH + '/tools/util/copy_res.bat', this.projectDir.getAbsolutePath() + '/../../../../zfres', this.projectDir.getAbsolutePath() + '/src/main/assets/zfres'
            }
        } else {
            exec {
                executable 'sh'
                args ZF_ROOT_PATH + '/tools/util/copy_res.sh', this.projectDir.getAbsolutePath() + '/../../../../zfres', this.projectDir.getAbsolutePath() + '/src/main/assets/zfres'
            }
        }
    }
}
preBuild.dependsOn ZFSetup

gradle.buildFinished {
    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
        exec {
            executable 'cmd.exe'
            args '/c', ZF_ROOT_PATH + '/tools/spec/Android/libs_copy.bat', 'ZFUIWidget', this.projectDir.getAbsolutePath() + '/..'
        }
    } else {
        exec {
            executable 'sh'
            args ZF_ROOT_PATH + '/tools/spec/Android/libs_copy.sh', 'ZFUIWidget', this.projectDir.getAbsolutePath() + '/..'
        }
    }
}

